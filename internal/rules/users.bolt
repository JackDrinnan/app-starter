//
// users.bolt
//

path /users {
  read() { amMaintainer() }
  /{user} is User;
}

type User {
  provider: Provider
  created: FinalNow
  lastActivity: CurrentTimestamp
  role: UserRole
  isElevated: Boolean | Null
  read() { key() == me() }
  write() { key() == me() && this != null }
}

type Provider extends String {
  validate() { this.test(/^(Username|Google|Facebook|Twitter|Github)$/) }
}

type UserRole extends String {
  write() { true }
  validate() { root.roles[$user] == this || (root.roles[$user] == null && this == 'User') }
}

type IsModerator extends Boolean {
  validate() { this == isModerator($user) }
}
type IsMaintainer extends Boolean {
  validate() { this == isMaintainer($user) }
}
type IsAdmin extends Boolean {
  validate() { this == isAdmin($user) }
}
type IsSuperAdmin extends Boolean {
  validate() { this == isSuperAdmin($user) }
}
type IsRoot extends Boolean {
  validate() { this == isRoot($user) }
}

isElevated(user) { root.users[user].isElevated }
amElevated() { isElevated(me()) }
