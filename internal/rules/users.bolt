//
// users.bolt
//

path /users {
  read() { amMaintainer() }
  /{user} is User;
}

type User {
  provider: Provider
  lastActivity: CurrentTimestamp
  isModerator:    IsModerator     | Null
  isMaintainer:   IsMaintainer    | Null
  isAdmin:        IsAdmin         | Null
  isSuperAdmin:   IsSuperAdmin    | Null
  isRoot:         IsRoot          | Null
  isElevated:     Boolean         | Null
  role:           UserRole
  read() { key() == me() }
  write() { key() == me() && this != null }
}

type Provider extends String {
  validate() { this.test(/^(Username|Google|Facebook|Twitter|Github)$/) }
}

type IsModerator extends Boolean {
  validate() { this == isModerator($user) }
}
type IsMaintainer extends Boolean {
  validate() { this == isMaintainer($user) }
}
type IsAdmin extends Boolean {
  validate() { this == isAdmin($user) }
}
type IsSuperAdmin extends Boolean {
  validate() { this == isSuperAdmin($user) }
}
type IsRoot extends Boolean {
  validate() { this == isRoot($user) }
}

type UserRole extends String {
  validate() { this.test(/^(Root|Super Admin|Admin|Maintainer|Moderator|User)$/) }
}

isElevated(user) { root.users[user].isElevated }
amElevated() { isElevated(me()) }
